<?xml version="1.0" ?>
<package>
<job>
<runtime>
<description><![CDATA[
Test suite for VBSCode
]]></description>
<example><![CDATA[
Examples:

- Read from the file INPUTFILE and print to the standard output
  cscript //nologo test-beautify.wsf < INPUTFILE

- Read from the file INPUTFILE and redirect output to another one
  cscript //nologo test-beautify.wsf < INPUTFILE > OUTPUTFILE

]]></example>
</runtime>
<script language="vbscript"><![CDATA[

' Very draft version for beautifying the vbscript code

Option Explicit

Private re_comment_or_continue
Set re_comment_or_continue = New RegExp
re_comment_or_continue.Global = True
re_comment_or_continue.IgnoreCase = True
re_comment_or_continue.Pattern = """[^""]*""" & _
	"|[\s:]*('|\brem\b).*" & _
	"|(\w\s|\W)\s*_$"

' Remove comments or line continuation. The removed line
' continuation is replaced with the single white space. In other
' cases the line is ended with the colon ":".
Private Function remove_comment_and_continuation(text)
	Dim eol, matches, match

	eol = ":"

	Set matches = re_comment_or_continue.Execute(text)
	For Each match in matches
		' Non-empty submatches(0) refers to the comment
		If match.SubMatches(0) <> "" Then
			text = Left(text, match.FirstIndex)
			Exit For
		End If
		' Non-empty submatches(1) refers to the line continuation
		If match.SubMatches(1) <> "" Then
			eol = " "
			text = Left(text, match.FirstIndex) & _
				match.SubMatches(1)
			Exit For
		End If
	Next

	If text <> "" Then
		text = text & eol
	End If

	remove_comment_and_continuation = text
End Function

Function beautify(text)
	Dim lines, i, line, chunks, chunk

	' For sure that splitting on CRLF or LF
	text = Replace(text, Chr(13), Chr(10))

	lines = Split(text, Chr(10))
	For i = 0 To UBound(lines)
		line = lines(i)
		line = remove_comment_and_continuation(line)
		lines(i) = line
	Next

	text = Join(lines, "")

	chunks = Split(text, """")
	For i = 0 To UBound(chunks) Step 2
		chunk = chunks(i)
		chunk = Replace(chunk, ":", Chr(13) & Chr(10))
		chunks(i) = chunk
	Next

	beautify = Join(chunks, """")
End Function

]]></script>
<script language="vbscript"><![CDATA[

Dim text

text = WScript.StdIn.ReadAll()
text = beautify(text)

WScript.StdOut.Write(text)

]]></script>
</job>
</package>
